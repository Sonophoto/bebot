# Copyright 2012, Dave Ray
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


setup {
  source common.soar

  sp {apply*init*add-state-name
    (state <s> ^operator.name init)
  -->
    (<s> ^name test-do)
  }

  sp {apply*do-func
    (state <s> ^operator <o>)
    (state <top-state> ^superstate nil)
    (<o> ^name do-func
         ^index { <index> < 10 }
         ^return-to <return-to>)
  -->
    (<s> ^<return-to> continue)
    (<top-state> ^did <index>)
  }

  sp {apply*do-func*break-at-10
    (state <s> ^operator <o>)
    (state <top-state> ^superstate nil)
    (<o> ^name do-func
         ^index 10
         ^return-to <return-to>)
  -->
    (<s> ^<return-to> break)
    (<top-state> ^did 10)
  }

  sp {propose*do
    (state <s> ^name test-do
               ^times <times>
              -^do-result)
  -->
    (<s> ^operator <o>)
    (<o> ^name bebot*do
         ^times <times>
         ^func do-func
         ^return-to do-result)
  }
}

test "can repeat an action several times" {
  sp {apply*init
    (state <s> ^operator.name init)
  -->
    (<s> ^times 8)
  }

  sp {pass
    (state <s> ^name test-do
     ^did 0 1 2 3 4 5 6 7
     ^do-result 8)
   -(state <other> ^name bebot*do)
  -->
    (pass)
  }
}

test "can repeat an action zero times" {
  sp {apply*init
    (state <s> ^operator.name init)
  -->
    (<s> ^times 0)
  }

  sp {pass
    (state <s> ^name test-do
     -^did
     ^do-result 0)
   -(state <other> ^name bebot*do)
  -->
    (pass)
  }
}

test "can break if function returns 'break'" {
  sp {apply*init
    (state <s> ^operator.name init)
  -->
    (<s> ^times 20)
  }

  sp {pass
    (state <s> ^name test-do
     ^did 0 1 2 3 4 5 6 7 8 9 10
     ^do-result 10)
   -(state <other> ^name bebot*do)
  -->
    (pass)
  }
}
