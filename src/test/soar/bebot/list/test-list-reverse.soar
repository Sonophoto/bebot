# Copyright 2012, Dave Ray
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

setup {
  source ../common.soar

  sp {proposose*reverse
    (state <s> ^name test-list-reverse
               ^input <input>)
  -->
    (<s> ^operator <o>)
    (<o> ^name bebot*list*reverse
         ^head <input>)
  }
}

test "the reverse of an empty list is an empty list" {

  sp {apply*init
    (state <s> ^operator.name init)
  -->
    (<s> ^name test-list-reverse
         ^input nil)
  }

  sp {pass
    (state <s> ^name test-list-reverse
               ^result nil)
  -->
    (pass)
  }

}

test "the reverse of a one-element list is a one element list" {
  sp {apply*init
    (state <s> ^operator.name init)
  -->
    (<s> ^name test-list-reverse
         ^input (list one))
  }

  sp {pass
    (state <s> ^name test-list-reverse
               ^result <r>)
    (<r> ^value one 
         ^next nil)
  -->
    (pass)
  }
}

test "can reverse a many element list" {
  sp {apply*init
    (state <s> ^operator.name init)
  -->
    (<s> ^name test-list-reverse
         ^input (list one two three four))
  }

  sp {pass
    (state <s> ^name test-list-reverse
               ^result <r0>)
    (<r0> ^value four 
          ^next <r1>)
    (<r1> ^value three 
          ^next <r2>)
    (<r2> ^value two 
          ^next <r3>)
    (<r3> ^value one 
          ^next nil)
  -->
    (pass)
  }
}

