# Copyright 2012, Dave Ray
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

sp {bebot*elaborate*library-info
  "Build initial library info on the top-state."
  (state <s> ^superstate nil)
-->
  (<s> ^bebot*library <l>)
  (<l> ^operators <ops>)
}

sp {bebot*elaborate*copy-library-info
  "Copy library info to sub-states."
  (state <s> ^superstate.bebot*library <l>)
-->
  (<s> ^bebot*library <l>)
}

sp {bebot*elaborate*operator*default-args
  "When a bebot operator is active on the superstate, fill in default values
   for any missing parameters using bebot*library info."
  (state <s> ^superstate.operator <o>
             ^bebot*library.operators <ops>)
  (<ops> ^<name>.default.<arg> <value>)
  (<o> ^name <name>
      -^<arg>)
-->
  (<s> ^name <name>)
  (<s> ^<arg> <value>)
}

sp {bebot*elaborate*operator*args
  "When a bebot operator is active on the superstate, copy existing parameters
   from the operator to the sub-state using bebot*library info."
  (state <s> ^superstate.operator <o>
             ^bebot*library.operators <ops>)
  (<ops> ^<name>.<< required default >>.<arg>)
  (<o> ^name <name>
       ^<arg> <value>)
-->
  (<s> ^name <name>)
  (<s> ^<arg> <value>)
}

sp {bebot*common*elaborate*named-function*with*args
  "For bebot operators that apply a function, if only an operator name is given,
   construct the function structure expected by those operators."
  (state <s> ^name << bebot*list*filter bebot*list*map bebot*list*fold-left >>
             ^func { <func-name> <=> string })
-->
  (<s> ^func <f>)
  (<f> ^name <func-name>)
}

sp {bebot*common*elaborate*copy*function*proposal*args
  "For bebot operators that apply a function, copy any non-name arguments
   from the function parameter (func) to the operator proposal. This allows
   for a very rudimentary form of currying."
  (state <s> ^func <func>
             ^name <bebot-op-name>
             ^operator <o>
             ^bebot*library.operators.<bebot-op-name>)
  (<func> ^name <name>
          ^args.{<arg> <> name} <value>)
  (<o> ^name <name>)
-->
  (<o> ^<arg> <value>)
}

source functions.soar
source bin.soar
source do.soar
source list/list.soar
source smem/smem.soar

