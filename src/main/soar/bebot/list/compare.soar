# Copyright 2012, Dave Ray
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

##########################################################################
# Operator bebot*list*compare - compare two lists
#
# ^operator
#   ^name bebot*list*compare
#   ^a <head-of-list-a>
#   ^b <head-of-list-b>
#   ^return-to <name-of-result-attribute> (defaults to "result")

sp {bebot*list*compare*elaborate*info
  "Add info about this operator to the lib*library structure on the
   top state."
  (state <s> ^superstate nil
             ^bebot*library.operators <ops>)
-->
  (<ops> ^bebot*list*compare <args>)
  (<args> ^default <def> 
          ^required <req>)
  (<def> ^return-to result)
  (<req> ^a nil
         ^b nil)
}

sp {bebot*list*compare*propose*init
  (state <s> ^name bebot*list*compare 
            -^ca 
            -^cb)
-->
  (<s> ^operator.name bebot*list*compare-init)
}

sp {bebot*list*compare*apply*init
  (state <s> ^name bebot*list*compare
             ^operator.name bebot*list*compare-init
             ^a <a>
             ^b <b>)
-->
  (<s> ^ca <a>
       ^cb <b>)
}

sp {bebot*list*compare*propose*values*return-false
  (state <s> ^name bebot*list*compare 
             ^ca <ca>
             ^cb <cb>)
  (<ca> ^value <va>)
  (<cb> ^value <> <va>)
-->
  (<s> ^operator <o>)
  (<o> ^name bebot*list*compare-return ^result false)
}

sp {bebot*list*compare*propose*end-of-b*return-false
  (state <s> ^name bebot*list*compare 
             ^ca.next
             ^cb nil)
-->
  (<s> ^operator <o>)
  (<o> ^name bebot*list*compare-return ^result false)
}

sp {bebot*list*compare*propose*end-of-a*return-false
  (state <s> ^name bebot*list*compare 
             ^ca nil
             ^cb.next)
-->
  (<s> ^operator <o>)
  (<o> ^name bebot*list*compare-return ^result false)
}

sp {bebot*list*compare*propose*return-true
  (state <s> ^name bebot*list*compare 
             ^ca nil
             ^cb nil)
-->
  (<s> ^operator <o>)
  (<o> ^name bebot*list*compare-return ^result true)
}

sp {bebot*list*compare*propose*continue
  (state <s> ^name bebot*list*compare 
             ^ca.value <value>
             ^cb.value <value>)
-->
  (<s> ^operator.name bebot*list*compare-continue)
}

sp {bebot*list*compare*apply*continue
  (state <s> ^name bebot*list*compare
             ^operator.name bebot*list*compare-continue
             ^ca <ca>
             ^cb <cb>)
  (<ca> ^next <an>)
  (<cb> ^next <bn>)
-->
  (<s> ^ca <ca> - <an>
       ^cb <cb> - <bn>)
}

sp {bebot*list*compare*apply*return
  (state <s> ^name bebot*list*compare 
             ^superstate <ss>
             ^return-to <return-to>
             ^operator <o>)
  (<o> ^name bebot*list*compare-return 
       ^result <result>)
-->
  (<ss> ^<return-to> <result>)
}

