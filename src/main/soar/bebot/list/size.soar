# Copyright 2012, Dave Ray
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

##########################################################################
# Operator bebot*list*size - get the size of a list
#
# ^operator
#   ^name bebot*list*last
#   ^head <head-of-list> (nil for empty list)
#   ^return-to <name-of-result-attribute>

sp {bebot*list*size*elaborate*info
  "Add info about this operator to the bebot*library structure on the
   top state."
  (state <s> ^superstate nil
             ^bebot*library.operators <ops>)
-->
  (<ops> ^bebot*list*size <args>)
  (<args> ^default <def> 
          ^required <req>)
  (<def> ^return-to result)
  (<req> ^head nil)
}

sp {bebot*list*size*propose*init
  (state <s> ^name bebot*list*size 
            -^current)
-->
  (<s> ^operator.name bebot*list*size-init)
}

sp {bebot*list*size*apply*init
  (state <s> ^name bebot*list*size
             ^operator.name bebot*list*size-init
             ^head <head>)
-->
  (<s> ^current <head> 
       ^count 0)
}

sp {bebot*list*size*propose*process
  (state <s> ^name bebot*list*size 
             ^current)
-->
  (<s> ^operator.name bebot*list*size-process)
}

sp {bebot*list*size*apply*process
  (state <s> ^name bebot*list*size
             ^operator.name bebot*list*size-process
             ^current <current>
             ^count <count>)
  (<current> ^next <next>)
-->
  (<s> ^current <current> - <next>
       ^count <count> - (+ <count> 1))
}

sp {bebot*list*size*apply*process-return
  (state <s> ^name bebot*list*size 
             ^operator.name bebot*list*size-process
             ^superstate <ss>
             ^return-to <return-to>
             ^current nil
             ^count <count>)
-->
  (<ss> ^<return-to> <count>)
}
          
