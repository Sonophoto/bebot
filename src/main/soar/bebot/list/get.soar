# Copyright 2012, Dave Ray
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

##########################################################################
# Operator bebot*list*get - get the kth value of a list
#
# ^operator
#   ^name bebot*list*get
#   ^head <head-of-list>
#   ^index desired index
#   ^return-to <name-of-result-attribute>

sp {bebot*list*get*elaborate*info
  "Add info about this operator to the lib*library structure on the
   top state."
  (state <s> ^superstate nil
             ^bebot*library.operators <ops>)
-->
  (<ops> ^bebot*list*get <args>)
  (<args> ^default <def> 
          ^required <req>)
  (<def> ^return-to result)
  (<req> ^head nil
         ^index nil)
}

sp {bebot*list*get*propose*advance
  (state <s> ^name bebot*list*get
             ^head <head>
             ^index <index>
            -^result)
-->
  (<s> ^operator <o>)
  (<o> ^name bebot*list*advance
       ^count <index>
       ^head <head>)
}

sp {bebot*list*get*propose*return
  (state <s> ^name bebot*list*get
             ^result)
-->
  (<s> ^operator.name bebot*list*get-return)
}

sp {bebot*list*get*elaborate*return-nil
  (state <s> ^name bebot*list*get
             ^result.head nil)
-->
  (<s> ^return nil)
}

sp {bebot*list*get*elaborate*return-non-nil
  (state <s> ^name bebot*list*get
             ^result.head.value <value>)
-->
  (<s> ^return <value>)
}

sp {bebot*list*get*apply*return
  (state <s> ^name bebot*list*get
             ^operator.name bebot*list*get-return
             ^superstate <ss>
             ^return <value> 
             ^return-to <return-to>)
-->
  (<ss> ^<return-to> <value>)
}

