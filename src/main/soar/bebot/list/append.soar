# Copyright 2012, Dave Ray
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# See append-common.soar

sp {bebot*list*append*propose*copy-first
  "Copy the first list by applying identity map"
  (state <s> ^name bebot*list*append 
             ^first {<first> <> nil}
             ^second <> nil
            -^copy-of-first)
-->
  (<s> ^operator <o>)
  (<o> ^name bebot*list*map
       ^func bebot*identity
       ^head <first>
       ^return-to copy-of-first)
}

sp {bebot*list*append*propose*append-fast
  "Now that the first list is copied, delegate to append-fast to destructively
   append the second to the copy of the first"
  (state <s> ^name bebot*list*append
             ^copy-of-first <first>
             ^second <second>
            -^result <result>)
-->
  (<s> ^operator <o>)
  (<o> ^name bebot*list*append-fast
       ^first <first>
       ^second <second>)
}

sp {bebot*list*append*propose*return
  "When append-fast has completed, return the new list."
  (state <s> ^name bebot*list*append
             ^result <result>)
-->
  (<s> ^operator <o>)
  (<o> ^name bebot*list*append*return
       ^result <result>)
}

